#include "../Shaders/DepthOfFieldCommon.hlsl"

#pragma kernel DOFCombine

#pragma multi_compile _ NEAR
#pragma multi_compile _ FAR

#define GROUP_SIZE 8

TEXTURE2D(_InputTexture);
TEXTURE2D(_InputNearTexture);
TEXTURE2D(_InputNearAlphaTexture);
TEXTURE2D(_InputFarTexture);
TEXTURE2D(_InputCoCTexture);
RWTexture2D<float3> _OutputTexture;
float4 _TargetScale;

[numthreads(GROUP_SIZE, GROUP_SIZE, 1)]
void DOFCombine(uint3 dispatchThreadId : SV_DispatchThreadID) {
	float2 uv = dispatchThreadId.xy / (_CameraBufferSize.zw * _TargetScale.y);
	float2 texelSize = _CameraBufferSize.xy * _TargetScale.x;
	float3 outColor = SAMPLE_TEXTURE2D_LOD(_InputTexture, sampler_linear_clamp, uv, 0).xyz;
#if defined(FAR)
	float3 dstColor = 0.0;
	float3 dstAlpha = 1.0;
	float3 farColor = SAMPLE_TEXTURE2D_LOD(_InputFarTexture, sampler_linear_clamp, uv, 0).xyz;
	float coc = SAMPLE_TEXTURE2D_LOD(_InputCoCTexture, sampler_linear_clamp, uv, 0).x;
	if (coc > 0.0) {
		//no-linear blend from "CryEngine 3 Graphics Gems" [Sousa13]
		float blend = sqrt(coc * Two_PI * 2.0);
		dstColor = farColor * saturate(blend);
		dstAlpha = saturate(1.0 - blend);
	}
	outColor = outColor * dstAlpha + dstColor;
#endif
#if defined(NEAR)
	float3 nearColor = SAMPLE_TEXTURE2D_LOD(_InputNearTexture, sampler_linear_clamp, uv, 0).xyz;
	float alpha = SAMPLE_TEXTURE2D_LOD(_InputNearAlphaTexture, sampler_linear_clamp, uv, 0).x;
	outColor = lerp(outColor, nearColor, alpha);
#endif
	_OutputTexture[dispatchThreadId.xy] = outColor;
}
